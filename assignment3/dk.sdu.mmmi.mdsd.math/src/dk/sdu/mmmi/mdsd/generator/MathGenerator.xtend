/*
 * generated by Xtext 2.25.0
 */
package dk.sdu.mmmi.mdsd.generator

import dk.sdu.mmmi.mdsd.math.Div
import dk.sdu.mmmi.mdsd.math.LetBinding
import dk.sdu.mmmi.mdsd.math.MathExp
import dk.sdu.mmmi.mdsd.math.MathNumber
import dk.sdu.mmmi.mdsd.math.Minus
import dk.sdu.mmmi.mdsd.math.Mult
import dk.sdu.mmmi.mdsd.math.Plus
import dk.sdu.mmmi.mdsd.math.VarBinding
import dk.sdu.mmmi.mdsd.math.VariableUse
import java.util.HashMap
import java.util.Map
import javax.swing.JOptionPane
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import dk.sdu.mmmi.mdsd.math.External
import dk.sdu.mmmi.mdsd.math.Parameter
import dk.sdu.mmmi.mdsd.math.ExternalUse
import dk.sdu.mmmi.mdsd.math.Expression

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class MathGenerator extends AbstractGenerator {
	
	static Map<String, String> variables;
	static boolean withLet = false;
	static int counter = 0;
	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		withLet = false
		counter = 0
		variables = new HashMap()
		val math = resource.allContents.filter(MathExp).next
		fsa.generateFile('math_expression/' + math.name + ".java", math.compile)
		
	}
	
	def compile(MathExp math)
	'''
	package math_expression;
	public class «math.name» {
		«FOR variable: math.variables»
		public int «variable.name»;
		«ENDFOR»
		
		«IF math.externals.size > 0»
		private External external;
			public «math.name»(External external) {
				this.external = external;
			}
		«ELSE»
		public «math.name»() {}
		«ENDIF»
	  
		public void compute() {
	  	
	  	«FOR variable:math.variables»
	  		«variable.name» = «variable.expression.computeExpression»;
	  	«ENDFOR»
	    }
	    «IF withLet»
	    interface LetBinding {
	        public int getResult();
	    }
	    «ENDIF»
	    «IF math.externals.size > 0»public interface External {
		«FOR external:math.externals»public int «external.id»(«FOR param:external.parameters SEPARATOR ', '»«param.param» param«counter = counter + 1»«ENDFOR»);«ENDFOR»
	    }
	    «ENDIF»
	}
	'''
	

	
	def static dispatch String computeExpression(VarBinding binding) {
		variables.put(binding.name, binding.expression.computeExpression())
		binding.name
	}
	
	def static dispatch String computeExpression(MathNumber exp) {
		exp.value.toString
	}

	def static dispatch String computeExpression(Plus exp) {
		"(" + exp.left.computeExpression + "+" + exp.right.computeExpression + ")"
	}
	
	def static dispatch String computeExpression(Minus exp) {
		"(" + exp.left.computeExpression + "-" + exp.right.computeExpression + ")"
	}
	
	def static dispatch String computeExpression(Mult exp) {
		"(" + exp.left.computeExpression + "*" + exp.right.computeExpression + ")"
	}
	
	def static dispatch String computeExpression(Div exp) {
		"(" + exp.left.computeExpression + "/" + exp.right.computeExpression + ")"
	}
	
	def static dispatch String computeExpression(ExternalUse exp) {
		var params = ""
		for (Expression param : exp.parameters){
			if (params.length <= 0){
				params = params + param.computeExpression
			} else {
				params = params + "," + param.computeExpression
			}
		}
		"external." + exp.id + "(" + params + ")"
	}

	def static dispatch String computeExpression(LetBinding exp) {
		withLet = true;
		"new LetBinding() {
		public int getResult(){
					int let"  + exp.name +  " = " + exp.binding.computeExpression + ";
					int "  + exp.name +  " = let"  + exp.name + ";
					return " + exp.body.computeExpression + ";
				}
			}.getResult()"
	}
	
	def static dispatch String computeExpression(VariableUse exp) {
		"(" + exp.ref.computeBinding + ")"
	}

	def static dispatch String computeBinding(VarBinding binding){
		if(!variables.containsKey(binding.name))
			binding.computeExpression()			
		"(" + binding.name + ")"
	}
	
	def static dispatch String computeBinding(LetBinding binding){
		binding.name
		
	}
	
	
}
